plugins {
    id 'scala'
    id 'maven'
}

group 'com.github.IS-BODAND'
version '1.1.3'

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Scala - Standard library
    compile group: 'org.scala-lang', name: 'scala-library', version: '2.12.6'
    // Scala XML - XML library
    compile group: 'org.scala-lang.modules', name: 'scala-xml_2.12', version: '1.1.0'
    // JSON Simple - JSON library
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    // Logback - Logger
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    // Apache Commons IO - IO tools
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    // Apache Commons Validator - Validating data
    compile group: 'commons-validator', name: 'commons-validator', version: '1.6'
    // Apache Commons CLI - Command Line Parameter Parser
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    // Apache HttpComponents HttpClient - HTTP library
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    // Jetbrains Annotations - Annotating
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    // jSoup - HTML Parser
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'
    
    // Scala test
    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: '3.0.5'
    // jUnit
    testCompile group: 'junit', name: 'junit', version: '4.12'
    // Scalactic
    testCompile group: 'org.scalactic', name: 'scalactic_2.12', version: '3.0.5'
}

jar {
    baseName = "nhstatistics-core"

    manifest {
        attributes "Main-Class": "org.bitbucket.bodand.nhscore.MainObj"
        attributes "Name": 'NHentaiStatistics'
        attributes "Specification-Title": 'Statistics app about nhentai doujinshi'
        attributes "Specification-Version": "$version"
        attributes "Specification-Vendor": "InfoSoft Corporation"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task createBat() {
    doLast {
        file("$buildDir/libs/nhs.bat").text = """@echo off
java -jar ${jar.baseName}-${version}.jar %*"""
    }
}

jar.finalizedBy createBat

task cutedoc(type: Exec, dependsOn: [scaladoc, classes]) {
    workingDir "$rootDir"

    commandLine "cutedoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    classifier = 'javadoc'
    from scaladoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives scaladocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}
